/*--------------------------------------------[REXX]
| Made with ZAOC by wizard@zdevops.com             |
| Puzzle: https://adventofcode.com/2025/day/8      |
+-------------------------------------------------*/
say "Advent of Code 2025 day 8"
NUMERIC DIGITS 14

x = bpxwunix('cat /prj/repos/ZAOC/puzzles/y2025d8',,file.,se.)

start = time('R')

answer = 0
circuit_size. = 0
in_circuit. = 0
circuit_count = 0
connected. = 0
distances. = 0

/* Calculate all distances */
do i = 1 to file.0
  if i // 10  = 0 then
    say TIME() "D calc, i="i "elaps:" TIME('E')
  do j = 1 to file.0
    if i = j then iterate
    if distances.i.j /= 0 then iterate
    d = distance(file.i,file.j)
    distances.i.j = d
    distances.j.i = d
  end
end

say TIME() "all d calculated"

do howmany = 1 to 1001
  say TIME() "cycle" howmany TIME('E')

  /* Find shortest unconnected pair */
  shortest = 10000000000000000000000000
  ci = 0
  cj = 0

  do i = 1 to file.0
    do j = i+1 to file.0
      if connected.i.j = 1 then iterate
      d = distances.i.j
      if d < shortest then do
        shortest = d
        ci = i
        cj = j
      end
    end
  end

  /* Mark this connection as used */
  connected.ci.cj = 1
  connected.cj.ci = 1

  ci_circuit = in_circuit.ci
  cj_circuit = in_circuit.cj

  if ci_circuit = 0 & cj_circuit = 0 then do
    /* Both nodes are new new circuit */
    circuit_count = circuit_count + 1
    in_circuit.ci = circuit_count
    in_circuit.cj = circuit_count
    circuit_size.circuit_count = 2
  end
  else if ci_circuit = 0 then do
    /* ci is new add to cj */
    in_circuit.ci = cj_circuit
    circuit_size.cj_circuit = circuit_size.cj_circuit + 1
  end
  else if cj_circuit = 0 then do
    /* same but then cj ci */
    in_circuit.cj = ci_circuit
    circuit_size.ci_circuit = circuit_size.ci_circuit + 1
  end
  else if ci_circuit /= cj_circuit then do
    /* merge da circutis */
    do node = 1 to file.0
      if in_circuit.node = cj_circuit then do
        in_circuit.node = ci_circuit
        circuit_size.ci_circuit = circuit_size.ci_circuit + 1
      end
    end
    circuit_size.cj_circuit = 0
  end

  if circuit_size.ci_circuit = file.0 then do
    say TIME() "All nodes connected in circuit" ci_circuit "at iteration" howman
    leave
  end
end

active_circuits = 0
circuit_map. = 0

do node = 1 to file.0
  c = in_circuit.node
  if c > 0 & circuit_map.c = 0 then do
    active_circuits = active_circuits + 1
    circuit_map.c = active_circuits
  end
end

circuits. = ''
circuits.0 = active_circuits

do node = 1 to file.0
  c = in_circuit.node
  if c > 0 then do
    mapped = circuit_map.c
    circuits.mapped = circuits.mapped node
  end
end

/* Display results */
sizes = ''
do u = 1 to circuits.0
  say u "->" circuits.u
  sizes = sizes words(circuits.u)
end

say sizes

/* need to still...... get the 3 largest ones from sizes */
elaps = time('E')-start
say 'Part One:' answer
say 'Elapse: 'elaps

exit


distance: procedure
  parse arg loc1, loc2
  parse var loc1 lx1","ly1","lz1
  parse var loc2 lx2","ly2","lz2
  dx = lx1 - lx2
  dy = ly1 - ly2
  dz = lz1 - lz2

  ans = (dx*dx) + (dy*dy) + (dz*dz)
  return sqrt(ans)

sqrt: procedure
  parse arg val
  xnew = val
  eps = 0.5 * 10**(1+fuzz()-digits())
  do until abs(xold-xnew) < (eps*xnew)
    xold = xnew
    xnew = 0.5 * (xold+val/xold)
  end

  return xnew

