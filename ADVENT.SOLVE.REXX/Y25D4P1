/*--------------------------------------------[REXX]
| Made with ZAOC by wizard@zdevops.com             |
| Puzzle: https://adventofcode.com/2025/day/4      |
+-------------------------------------------------*/
say "Advent of Code 2025 day 4"
x = bpxwunix('cat /prj/repos/ZAOC/puzzles/y2025d4',,file.,se.)

grid. = 0
maxx = length(file.1)
maxy = file.0

/* Build our grid of paper rolls */
do i = 1 to file.0
  do j = 1 to length(file.i)
    grid.j.i = substr(file.i,j,1)
  end
end

part1 = 0
rolls = 0

do y = 1 to maxy
  l = ''
  do x = 1 to maxx
    if grid.x.y /= '@' then iterate /* only check rolls */
    rolls = rolls + 1 /* dunno if we need this */
    /* rolls with less than 4 neighbours can be lifted */
    if neighbours(x,y) < 4 then part1 = part1 + 1
  end
end

say "Part One:" part1 "rolls out of" rolls "can be moved"

exit

neighbours: procedure expose grid.
  parse arg sx,sy
  amt = 0
  yl1 = sy - 1
  yl2 = sy + 1
  xl1 = sx - 1
  xl2 = sx + 1
  do ny = yl1 to yl2
    do nx = xl1 to xl2
      if nx = sx & ny = sy then iterate      /* don't count ourself */
      if grid.nx.ny = '@' then amt = amt + 1 /* one more can be moved */
    end
  end
  return amt
